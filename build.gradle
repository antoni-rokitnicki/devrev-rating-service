plugins {
	id 'org.springframework.boot' version '2.1.5.RELEASE'
	id 'java'
	id "com.moowork.node" version "1.3.1"
    id 'net.ltgt.apt' version '0.20'
	id "com.palantir.docker" version "0.22.1"
}

apply plugin: 'io.spring.dependency-management'
apply plugin: 'net.ltgt.apt-idea'

group = 'pl.devrev'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

node {
    download = true
    workDir = file("${project.buildDir}/nodejs")
    npmWorkDir = file("${project.buildDir}/npm")
    yarnWorkDir = file("${project.buildDir}/yarn")
    nodeModulesDir = file("${project.projectDir}")
}

ext {
    mapstructVersion = '1.3.0.Final'
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.hibernate.validator:hibernate-validator:6.0.10.Final'
    implementation "org.mapstruct:mapstruct:${mapstructVersion}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
    testAnnotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	testCompile 'org.junit.platform:junit-platform-commons:1.4.1'
	testCompile 'org.junit.jupiter:junit-jupiter-api:5.4.2'
	testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.4.2'
}

task unpack(type: Copy) {
	dependsOn bootJar
	from(zipTree(tasks.bootJar.outputs.files.singleFile))
	into("build/dependency")
}

docker {
	dependsOn unpack
	name 'devrev-movies:1.0.0'
	copySpec.from(tasks.unpack.outputs).into("dependency")
	buildArgs(['DEPENDENCY': "dependency"])
	noCache true
}

test {
	useJUnitPlatform()
}